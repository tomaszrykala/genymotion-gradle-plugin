buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'nu.studer.plugindev'

/**
 * The following declaration allows to define the Android Gradle plugin version used during the integration tests
 * from outside the build.gradle, using the gradle property: 'androidPluginVersion'
 * It can be set from command line using:
 * gradle test -PandroidPluginVersion=1.3.1
 *
 * If not set, the value is '+', meaning we keep the latest version available on the repository (including beta).
 */
def androidVersion = "+"
if (hasProperty("androidPluginVersion")) {
    androidVersion = androidPluginVersion
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.11'
    testCompile 'org.answer-it:mock-slf4j:1.0.0'
    testCompile "com.android.tools.build:gradle:$androidVersion"
}

group = 'com.genymotion'
version = '1.1'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../repo'))

            pom.artifactId = 'plugin'
            pom.groupId = project.group
            pom.version = project.version
        }
    }
}

/**
 * JUnit does not allow to select the category to launch from the command line.
 * The following declaration enables this feature by using 2 gradle properties: 'includeCat' and 'excludeCat'
 * They can be set from command line using:
 * gradle test -PincludeCat=com.genymotion.MyCategory -PexcludeCat=com.genymotion.MyCategory
 */
test {
    useJUnit {
        if (project.hasProperty("includeCat")) {
            includeCategories includeCat
            println "includeCat $includeCat"
        }
        if (project.hasProperty("excludeCat")) {
            excludeCategories excludeCat
            println "excludeCat $excludeCat"
        }
    }
}

plugindev {
    pluginId 'com.genymotion.plugin'
    pluginName 'genymotion-gradle-plugin'
    pluginImplementationClass 'com.genymotion.GenymotionGradlePlugin'
    pluginDescription 'The Genymotion Gradle Plugin allows you to control your Genymotion instances just with your build.gradle file'
    pluginLicenses 'GPL-3.0'
    pluginTags 'plugin', 'gradle', 'android', 'genymotion', 'automation', 'test', 'unit test', 'functional test', 'CI', 'continuous integration', 'jenkins'
    authorId 'Genymobile'
    authorName 'Genymobile'
    authorEmail 'support@genymotion.com'
    projectUrl 'https://github.com/Genymobile/genymotion-gradle-plugin'
    projectInceptionYear '2015'
    done() // do not omit this
}

bintray {
    //Add these values to your ~/.gradle/gradle.properties file
    // user = "$BINTRAY_USER_GENY"
    user = "tomaszrykala"
    // key = "$BINTRAY_API_KEY_GENY"
    pkg.repo = 'Genymotion'
}
